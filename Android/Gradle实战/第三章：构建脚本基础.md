# 第三章 构建脚本基础
## 构建块
 每个Gradle构建都包括三个基本的构建块：项目（projects）、任务（tasks）、属性（propertities）,每个构建至少包括一个项目，项目包括一个或多个任务，项目和任务都有很多个属性来控制构建过程。  
 Gradle运用了领域驱动的设计理念（DDD）来给自己的领域构建软件建模，因此Gradle的项目和任务都在Gradle的API中有一个直接的class来表示。
### 项目
 在Gradle术语里，项目表示你想构建的一个组件（比如一个JAR文件）,或者你想完成的一个目标（比如打包app）。  
 每一个Gradle脚本至少定义了一个项目。当开始构建过程后，Gradle基于你的配置实例化org.gradle.api.Project这个类以及让这个项目通过project变量来隐式获得。  
 下图列出了API接口和最重要的方法:  
 ![Image](https://github.com/HousqLove/Reader/blob/fa28aff722276ab8642811f2f2da514fa69eb654/Android/Gradle%E5%AE%9E%E6%88%98/images/gradle-3-1.png)  
 一个项目可以创建新任务、添加依赖和配置、应用插件和其他脚本，许多属性比如name和description都是可以通过getter和setter方法来访问。  
 Project实例允许你访问你项目所以的Gradle特性，比如任务的创建和依赖了管理，记住一点当访问你项目的属性和方法时你并不需要显式的使用project变量（Gradle假定你的意思是Project实例）。如下：  
```
	setDescription("myProject")
	println "Description of project name:" + projet.description
```
 Gradle支持多项目的构建。当一个软件系统变得越复杂，你越想把它分解成一个个功能性的模块，模块之间可以互相依赖，每个模块有自己的build.gradle脚本
### 任务
 任务里有两个概念：任务动作（actions）和任务依赖.  
 Gradle API中任务的表示：org.gradle.api.Task 接口  
 ![image](https://github.com/HousqLove/Reader/blob/caffe981226f8e7afd9b693195ad5d94dffa2e2d/Android/Gradle%E5%AE%9E%E6%88%98/images/gradle-3-2.png)  
### 属性
 每个Project和Task实例都提供了setter和getter方法来访问属性。  
 Gradle允许你通过外部属性来定义自己的变量
### 外部属性
 外部属性一般存储在键值对中，要添加一个属性，你需要使用ext命名空间。如下：  
```
	//Only initial declaration of extra property requires you to use ext namespace
	project.ext.myProp = 'myValue'
	ext {
		someOtherProp = 123
	}

	//Using ext namespace to access extra property is optional
	assert myProp == 'myValue'
	println project.somaOtherProp
	ext.someOtherProp = 567
```
 外部属性可以定义在一个属性文件中：通过在/.gradle路径或项目根目录下的gradle.properties文件来定义属性可以直接注入到你的项目中，他们可以通过project实例访问。    
 你也可以通过下面的方法来定义属性：

* 通过-P命令行选项来定义项目属性
* 通过-D命令行来定义系统属性
* 环境属性遵循这个模式：``` ORG_GRADLE_PROJECT_projectName=sameValue```

## 管理任务
  每个新创建的任务都是org.gralde.api.DefaultTask类型，是org.gradle.api.Task的标准实现。
### 声明任务动作（actions）
 动作就是在你的任务中放置构建逻辑的地方，Task接口提供了两个方法声明任务的动作：doFirst和doLast，当任务执行的时候，定义在闭包里的动作就按顺序执行。  
 左移操作符```<<```是方法doLast的快捷键,他们的作用是一样的。  

 **给已经存在的任务添加动作**  

 对于一个任务，要么是第一个或者最后一个。实际上当任务创建的时候你可以添加任意多个动作，每一个任务都有一个动作清单，他们在运行的时候执行。  
```
	task printVersion {
		doFirst {
			println "Before reading"
		}

		doLast {
			println "Version: $version"
		}
	}
```
 你可以在任务的动作列表的最前面添加其他任务：  
``` printVersion.doFirst { println "first action"} ```

### 定义任务依赖
  ```dependsOn```方法用来声明一个任务依赖于一个或者多个任务。  

```
 	task first << { println "first" }
 	task second << { println "Second" }

 	//声明多个依赖
 	task printVersion(dependsOn: [second, first]) << {
 		...
 	}

 	task third << { ... }
 	third.dependsOn('printVersion')
```  
  Gradle并不能保证依赖的任务能够按顺序执行，dependsOn方法只是定义这些任务应该在这个任务之前执行，并不关心任务具体怎么执行。  
### 终结者任务
 你可能需要在一个任务执行之后进行一些清理工作，Gradle提供了一个finalizer任务来实现这个功能.你可以用```finalizedBy```方法来结束一个指定的任务：  
```
	task first << { println "first" }
	task second << { println "second" }
	//声明first结束后执行second任务
	first,finalizedBy second
```
### 任务配置块
 Gradle有称为任务配置块的代码块。  
```
	task loadVersion {
		project.version = new ProjectVersion(0, 1)
	}
```
 任务配置块总是在任务动作之前执行。理解这个行为就要理解Gradle的构建声明周期.
### Gradle的构建声明周期
 执行gradle build都会经过三个不同的阶段：初始化、配置、执行。  

 在初始化阶段，Gradle给你的项目创建一个Project实例，你的构建脚本只定义了单个项目，在多项目的上下文环境中，初始化的阶段尤为重要。根据你正在执行的项目，Gradle找出这个项目的依赖。  

 在配置阶段， Gradle构建一些在构建过程中需要的一些模型数据。  

 在执行阶段任务按顺序执行，执行顺序通过依赖关系决定的， 标记为up-to-date 的任务会跳过。
### 声明任务的输入和输出
 Gradle通过比较两次build直接输入和输出有没有变化来确定这个任务是否是最新的。如果从上一个执行之后这个任务的输入和输出没有发生改变，这个任务就标记为up-to-date，跳过这个任务。  

 输入和输出在DefaultTask类中用域来表示。
### 编写自定义任务
 Gradle会给每一个任务创建一个DefaultTask类型的实例。  
 当你需要创建一个自定义任务时，需要创建一个继承自DefaultTask的类。  
```
	class ReleaseVersionTask extends DefaultTask {
		//通过注解声明任务的输入和输出
		@Input Boolean release
		@Output File destFile

		ReleaseVersionTask() {
			//在构造器里设置任务的分组和描述
			group = 'versioning'
			description = 'Makes project a release version.'
		}

		//通过注解声明要执行的任务
		@TaskAction
		void start() {
			project.version.release = true
			ant.propertyfile(file: destFile) {
				entry(key: 'release', type: 'string', operation: '=', value: 'true')
			}
		}
	}
```
 任务输入和输出注解给你的实现添加了语法糖，他们和调用TasksInputs和TaskOutputs方法是一样的效果。  

 **使用自定义任务**  

 你需要在build脚本中创建一个RealeaseVersionTask类型的任务：
```
	task makeReleaseVersion(type: ReleaseVersionTask) {
		release = version.release
		destFile = version.versionFile
	}
```

### Gradle自带的任务类型
 Gradle 自带的任务类型继承自DefaultTask，Gradle提供了很多自带的任务类型。如：Zip，Copy等  

 **任务依赖推导**  

 通过一个任务的输出作为另一个任务的输入，依赖推导就出来了。

### 在buildSrc目录创建代码
 前面创建了ProjectVersion和ReleaseVersionTask两个类，这些类可以移动到项目的buildSrc目录。  
 buildSrc目录是一个放置源代码的可选目录。  
 Gradle采用标准的项目布局，java代码在src/main/java目录。Groovy代码应该在src/main/groovy目录，在这些目录的任何代码都会自动编译然后放置到项目的classpath目录。  
 如下:    
 ![Image](https://github.com/HousqLove/Reader/blob/d953bb2b6df1f834b19849007845b6f3e16b69f6/Android/Gradle%E5%AE%9E%E6%88%98/images/gradle-3-3.png)  
 源代码目录和在脚本中定义有点不一样，需要带入Gradle的API。如下：  
```
	package com.manning.gia
	import org.gradle.api.DefaultTask
	import org.gradle.api.tasks.Input
	import org.gradle.api.tasks.OutputFile
	import org.gradle.api.tasks.TaskAction
	class ReleaseVersionTask extends DefaultTask {
		(...)
	}
```
## 掌握构建生命周期
 生命周期事件可以在指定的生命周期之前、之中或之后发生，在执行阶段之后发生的生命周期事件就该是构建的完成了。  
 给构建生命周期事件添加回调有两种方法：一是通过闭包，二是实现Gradle API的一个监听器。  
 如下：  
 ![Image](https://github.com/HousqLove/Reader/blob/master/Android/Gradle%E5%AE%9E%E6%88%98/images/gradle-3-4.png)  
 在配置阶段，Gradle决定任务在执行阶段的执行顺序，依赖关系的内部结构是通过直接的无环图（DAG）来表示，可能通过dependsOn或者隐式的依赖推导来创建依赖关系。  
 DAG从来不会有环，即一个已经执行的任务不会再次执行。  
 构建系统准确知道在执行之前应该运行哪些任务，可以查询任务图来查看他是否存在，相关接口如下：  
 ![Image](https://github.com/HousqLove/Reader/blob/master/Android/Gradle%E5%AE%9E%E6%88%98/images/gradle-3-5.png)  

 **添加监听方法**  

 下面代码通过调用```whenReady```方法来注册回调接口，当任务图创建的时候这个回调会自动执行。  
```
	gradle.taskGraph.whenReady {
			TaskExecutionGraph taskGraph -> if(taskGraph.hasTask(release)) {
					if(!version.release) {
							version.release = true

							ant.propertyfile(file: versionFile){
									entry(key: 'release', type: 'string', operation: '=', value: 'true')
							}
					}
			}
	}
```
 也可以实现一个监听器实现同样的效果  
 首先在构建脚本中编写一个实现指定监听器的类，然后在构建中注册这个实现，监听任务执行图的接口是```TaskExecutionGraphListener```, 编写的时候只需要实现```graphPopulated```方法，如下图：  
 ![Image](https://github.com/HousqLove/Reader/blob/master/Android/Gradle%E5%AE%9E%E6%88%98/images/gradle-3-6.png)  
 编程实现：  
```
	class ReleaseVersionListener implements TaskExecutionGraphListener {
		final static String releaseTaskPath = ':release'

		@Override
		void graphPopulated(TaskExecutionPraph taskGraph) {
			//查看是否包含release任务
			if(taskGraph.hasTask(releaseTaskPath)) {
				List<Task> allTasks = taskGraph.allTasks
				//查找release任务
				Task releaseTask = taskGraph.find {it.path == releaseTaskPath}
				Project project = releaseTask.project

				if(!project.version.release) {

					project.version.release = true
					project.ant.propertyfile(file: project.versionFile) {
						entry(key: 'release', type: 'string', operation: '=', value: 'true')
					}
				}
			}
		}
	}

	def releaseVersionListener = new ReleaseVersion()
	//注册监听器
	gradle.taskGraph.addTaskExecutionGraphListener(releaseVersionListener)
```
